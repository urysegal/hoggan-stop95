CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C* THIS ROUTINE RETURNS A SET OF THREE-CENTER TWO-ELECTRON INTEGRALS *C
C* OVER A SLATER ORBITAL BAIS SET USING A TWO-RANGE ONE-CENTER       *C
C* EXPANSION METHOD.                                                 *C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      SUBROUTINE TRI32(NEPSIL, NTORB,NSUM, LM123,
     $                  NORB1,NORB2,NORB3, NLMA,NLMB,NLMC,
     $                   ZETAA,ZETAB,ZETAC, AB,THETAB,PHIAB,
     $                                   AC,THETAC,PHIAC, FATIMA)
      IMPLICIT REAL*8 (A-H, O-Z)
      COMPLEX*16 OUIZA
      INCLUDE "../lib95/SIZE.INCL"
      INCLUDE "../lib95/CONST.INCL"

      DIMENSION NLMA(*),NLMB(*),NLMC(*), ZETAA(*),ZETAB(*),ZETAC(*),
     $                                                     FATIMA(*)

      DIMENSION YLMAB(0:((LDEV+1)*(LDEV+2))/2),
     $          YLMAC(0:((LDEV+1)*(LDEV+2))/2)

      DIMENSION ARGNT1((L3MAX+1)*(2*L3MAX+1)),
     $          ARGNT2((L4MAX+1)*(2*L4MAX+1)),
     $          ARGNT3((2*L4MAX+1)**2 * (L3MAX+1)**3)

      DIMENSION RADAR((L1MAX+1)**4*(2*L1MAX+1)**2*(LMDMAX+1))

      DIMENSION OUIZA(20,20,20,20)

      COMMON/FACT0/FACT(0:30)
      COMMON/DFACT/DFAC(0:100)
      COMMON/GLE01/RLEG01(L001+L002+L003)
      COMMON/GLE02/RLEG02(L004+L005+L006)
      COMMON/BESS0/BESF01(2*LTOT6)
      COMMON/X4POW/X4NL(10*LTOT6)
      COMMON/GLAE0/RLEAG0(LMHERM), ISP
      COMMON/BESHER/HEREX(LTOT6*LMHERM), BESS01(LTOT6*(LDEV+1))
      COMMON/COMG00/GEGIN0(LMHERM*LMHERM*(LMDMAX+L1MAX+L2MAX+1))
      COMMON/COMG01/GEGIN1(LMHERM*(LDEV+1))
      COMMON/COMV00/VL((NORBM*(NORBM+1))/2*(L1MAX+L2MAX+1)*LEA04)
      COMMON/INTEGN/NINT
      EXTERNAL GLROOT


C.... DETERMINING MIN(AB, AC) AND MAX(AB, AC)

      RMIN = DMIN1(AB, AC)
      RMAX = DMAX1(AB, AC)

C.... COMPUTATION OF THE REQUIRED SPHERICAL HARMONICS

      CALL YLMV1(THETAB, THETAC, YLMAB, YLMAC)

C.... COLLECTING THE ROOTS OF THE GAUSS-LEGENDRE

      CALL DUMMY1(RMIN, RMAX, LEG05, LEG06)

C.... COMPUTATION OF SOME VALUES OF THE ONE-CENTER CHARGE DISTRIBUTION
C.... POTENTIAL

      CALL OCCDP(L1MAX+L2MAX, NORB1, NLMA, ZETAA, RLEAG0,LEG05+LEG06,
     $                                                     LEA04, VL)
C.... COMPUTATION OF THE MODIFIED BESSEL FUNCTION I ONCE AND FOR ALL OVER
C.... THE ROOTS OF THE GAUSS-LEGENDRE QUADRATURE
C.... SINCE, THE PARAMTER NONE IS SET TO A NEGATIVE INTEGER, THE
C.... PARAMETER DUMMY IS NOT USED AND THEREFORE IS ARBITRARY.

      DUMMY = 1.D0
      NONE  = -1

      IN = 1
      CALL ABIN02(DUMMY, AB, NONE, RLEG01,L1_3, RLEG02,L4_6,
     $                   RLEAG0, IN, LEA03, BESF01, X4NL, HEREX, BESS01)

C.... COMBINING THE GIVEN SET OF SLATER ORBITALS, BEGINING BY CONSIDERING
C.... THE TWO-CENTER CHARGE DISTRIBUTION

      DO 5 NAT4=1, NORB3
       LST   = 5*(NAT4-1)
       NO4   = NLMC(LST+2)
       N4    = NLMC(LST+3)
       L4    = NLMC(LST+4)
       M4    = NLMC(LST+5)
       ZETA4 = ZETAC(NAT4)

      DO 5 NAT3=1, NORB2
       KST   = 5*(NAT3-1)
       NO3   = NLMB(KST+2)
       N3    = NLMB(KST+3)
       L3    = NLMB(KST+4)
       M3    = NLMB(KST+5)
       ZETA3 = ZETAB(NAT3)

C.... COMPUTATION OF THE ROOTS OF THE GAUSS-LAGUERRE QUADRATURE WITH THE
C.... COEFFICIENTS OCCURING IN THE GENERALIZED GEGENBAUER ADDITION THEOREM

       ISP  = LEA04 - LAG04
       XXXX = DGLGQ(GLROOT, RMAX, ZETA3+ZETA4, LAG04)

       LMDL12 = LMDMAX+1
       CALL GEGEN4(LMDL12, N3-L3, ZETA3, AB, RLEG01, RLEG02, RLEAG0,
     $                                           BESF01, X4NL, GEGIN0)

       LMDL12 = LMDMAX+LM123+LM123+L3+L4+1
       CALL GEGEN4(LMDL12, N4-L4, ZETA4, AC, RLEG01, RLEG02, RLEAG0,
     $                                           BESF01, X4NL, GEGIN1)

C.... COMPUTATION OF THE COEFFICIENTS INVOLVED IN THE SOLID SPHERICAL
C.... HARMONICS ADDITION AND MULTIPLICATION THEOREM

       CALL HUSED(DFAC, L3,M3, L4,M4, AB,YLMAB, AC,YLMAC,
     $                                         ARGNT1, ARGNT2, ARGNT3)

C.... COMBINATION OF THE ORBITALS LEADING TO THE ONE-CENTER CHARGE POTENTIAL

      INDEX = 0

      DO 5 NAT2=1, NORB1
       JST   = 5*(NAT2-1)
       NO2   = NLMA(JST+2)
       N2    = NLMA(JST+3)
       L2    = NLMA(JST+4)
       M2    = NLMA(JST+5)
       ZETA2 = ZETAA(NAT2)

      DO 5 NAT1=NAT2, NORB1
       IST   = 5*(NAT1-1)
       NO1   = NLMA(IST+2)
       N1    = NLMA(IST+3)
       L1    = NLMA(IST+4)
       M1    = NLMA(IST+5)
       ZETA1 = ZETAA(NAT1)
cc      if(zeta1.gt.2.5d0.and.zeta1.lt.5.0d0)goto 5

C.... COMPUTATION OF THE ONE-CENTER CHARGE DISTRIBUTION POTENTIAL OVER THE
C.... GAUSS-LEGUERRE ROOTS

       N12    = N1+N2
       L12    = L1+L2
       ZETA12 = ZETA1+ZETA2
       CALL OCCDP1(L1MAX+L2MAX, INDEX, N12,L12, ZETA12, RLEAG0,
     $                                            LEG05+LEG06,LEA04, VL)

C.... COMPUTATION OF THE RADIAL INTEGRAL

       CALL RTCTEI(INDEX, L1,L2,L3,L4, ZETA3+ZETA4, RMIN,RMAX, RADAR)
       INDEX = INDEX + 1

C.... COMPUTATION OF THE TERMS (X1 X3 | X1 X4) AND (X1 X3 | X2 X4)
C.... COMPUTATION OF THE TERMS (X2 X3 | X1 X4)

       NINT = NINT + 1

       CALL TCTEI(NEPSIL, N1,L1,M1, N2,L2,M2, N3,L3,M3, N4,L4,M4,
     $      ZETA1,ZETA2,ZETA3,ZETA4, AB,YLMAB,PHIAB, AC,YLMAC,PHIAC,
     $                  ARGNT1, ARGNT2, ARGNT3, RADAR, TRI32R,TRI32I)

       OUIZA(NAT1,NAT2,NAT3,NAT4) = DCMPLX(TRI32R,  TRI32I)

c      WRITE(*, 2) N1,L1,M1, ZETA1
c      WRITE(*, 2) N2,L2,M2, ZETA2
c      WRITE(*, 2) N3,L3,M3, ZETA3
c      WRITE(*, 2) N4,L4,M4, ZETA4
c      write(*, 3) TRI32R,  TRI32I
c      write(*,*) "three-center coulomb"

 5    CONTINUE

 1    FORMAT(4(I2, 1X), 2(D20.14, 1X))
 2    FORMAT(3(I2, 1X), F7.5)
 3    FORMAT(2(D20.14, 1X))

      CALL SYMM05(NORB1,NORB2,NORB3, NLMA,NLMB,NLMC, OUIZA)
      CALL REAL05(NORB1,NORB2,NORB3, NLMA,NLMB,NLMC,
     $                                NTORB, NSUM, OUIZA, FATIMA)
  100 continue
      RETURN
      END
