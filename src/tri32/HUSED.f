CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C* THIS SUBROUTINE RETURNS THE COEFFICIENTS NEEDED IN THE SOLID      *C
C* SPHERICAL HARMONICS ADDITION AND MULTIPLICATION THEOREMS.         *C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      SUBROUTINE HUSED(DFAC, L3,M3, L4,M4, AB,YLMAB, AC,YLMAC,
     $                                          ARGNT1, ARGNT2, ARGNT3)
      IMPLICIT REAL*8 (A-H, O-Z)
      DIMENSION DFAC(0:*), YLMAB(0:*), YLMAC(0:*),
     $                       ARGNT1(*), ARGNT2(*),ARGNT3(*), ARGNT(100)


      ABLP = -AB
      K1   = 1

      DO 5 LP3=0, L3
       ABLP = -ABLP/AB
      DO 5 MP3=MAX(-LP3, -L3+LP3+M3), MIN(LP3, L3-LP3+M3)
       CALL GAUNT(LP3,MP3, L3,M3, LMIN1,LMAX1,MDF, NGAUNT, ARGNT)
       NYL        = ((L3-LP3)*(L3-LP3+1))/2 + IABS(M3-MP3)
       MUPW       = (M3-MP3 - IABS(M3-MP3))/2
       ARGNT1(K1) = (-1)**MUPW *
     $              ABLP*YLMAB(NYL)*ARGNT(1)/(DFAC(LP3)*DFAC(L3-LP3))
       K1         = K1 + 1
 5    CONTINUE

      ACLP = -AC
      K2   = 1

      DO 10 LP4=0, L4
       ACLP = -ACLP/AC
      DO 10 MP4=MAX(-LP4, -L4+LP4+M4), MIN(LP4, L4-LP4+M4)
       CALL GAUNT(LP4,MP4, L4,M4, LMIN2,LMAX2,MDF, NGAUNT, ARGNT)
       NYL        = ((L4-LP4)*(L4-LP4+1))/2 + IABS(M4-MP4)
       MUPW       = (M4-MP4 - IABS(M4-MP4))/2
       ARGNT2(K2) = (-1)**MUPW *
     $              ACLP*YLMAC(NYL)*ARGNT(1)/(DFAC(LP4)*DFAC(L4-LP4))
       K2         = K2 + 1
 10   CONTINUE


      K3 = 1

      DO 15 LP3=0, L3
      DO 15 MP3=MAX(-LP3, -L3+LP3+M3), MIN(LP3, L3-LP3+M3)
      DO 15 LP4=0, L4
      DO 15 MP4=MAX(-LP4, -L4+LP4+M4), MIN(LP4, L4-LP4+M4)
       CALL GAUNT(LP3,MP3, LP4,MP4, LMIN3,LMAX3,MDF, NGAUNT, ARGNT)
       KG = 1
      DO 15 LP34=IABS(LP3-LP4), LP3+LP4, 2
       IF(LP34 .GE. LMIN3)THEN
	ARGNT3(K3) = ARGNT(KG)
	KG         = KG + 1
       ELSE
	ARGNT3(K3) = 0.D0
       ENDIF
       K3 = K3 + 1
 15   CONTINUE

      RETURN
      END
