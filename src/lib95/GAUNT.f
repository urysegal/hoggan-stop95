C.... SUBROUTINE GAUNT(L2,M2,L3,M3,LARRAY,L1MIN,L1MAX,M1,NGAUNT,ARGNT)
      SUBROUTINE GAUNT(L2,M2,L3,M3,L1MIN,L1MAX,M1,NGAUNT,ARGNT)
C**********************************************************************
C
C  PROGRAM:  SUBROUTINE GAUNT.
C
C  PROGRAMMER:  ERNST JOACHIM WENIGER.
C
C  DATE:  REGENSBURG, 28 04 1981.
C
C
C  DESCRIPTION:
C  -----------
C
C  THIS SUBROUTINE CALCULATES A STRING OF GAUNT-COEFFICIENTS
C
C              ( L3 M3 / L2 M2 / L1 M1 )
C
C  FOR ALL ALLOWED L1-VALUES, WHERE L2, M2, L3 AND M3 ARE FIXED INPUT
C  QUANTITIES.
C
C  THIS YIELDS THE FOLLOWING CONDITIONS FOR L1 AND M1:
C
C  M1 = M3 - M2.
C
C  L1 = L1MIN, L1MIN + 2, ... , L1MAX - 2 , L1MAX.
C
C  L1MAX = L2 + L3.
C
C  L1MIN = MAX0(IABS(L2-L3),IABS(M1))
C          IF MAX0(IABS(L2-L3),IABS(M1)) + L1MAX IS EVEN AND
C  L1MIN = MAX0(IABS(L2-L3),IABS(M1)) + 1
C          IF MAX0(IABS(L2-L3),IABS(M1)) + L1MAX IS ODD.
C
C  THE RESULTING GAUNT-COEFFICIENTS ( L3 M3 / L2 M2 / L1 M3-M2 ) ARE
C  STORED IN THE 1-DIMENSIONAL ARRAY ARGNT AS ARGNT((L1-L1MIN)/2 + 1),
C  I.E. THE ARRAY ARGNT HAS TO BE AT LEAST OF LENGTH
C  NGAUNT = (L1MAX - L1MIN)/2 + 1.
C
C
C  IN THIS PROGRAM THE GAUNT-COEFFICIENTS ARE CALCULATED USING THEIR
C  REPRESENTATION IN TERMS 3JM-SYMBOLS ( M.ROTENBERG ET AL., THE 3J-
C  AND 6J-SYMBOLS, P.5, EQ.(1.19) ), I.E. THE GAUNT COEFFICIENTS ARE
C  REPRESENTED AS THE PRODUCT OF TWO 3JM-SYMBOLS. ONE - USUALLY
C  CALLED THE PARITY COEFFICIENT - POSSESSES ONLY MAGNETIC QUANTUM
C  NUMBERS M1, M2 AND M3 WITH M1 = M2 = M3 = 0. THE OTHER CONTAINS
C  MAGNETIC QUANTUM NUMBERS WHERE IN THE GENERAL CASE AT LEAST TWO
C  M-VALUES ARE DIFFERENT FROM ZERO.
C  ACCORDINGLY TWO STRINGS OF 3JM-SYMBOLS HAVE TO BE CALCULATED.
C  IN EITHER CASE THE 3JM-SYMBOLS WILL BE CALCULATED RECURSIVELY.
C
C  THE 3JM-SYMBOLS WITH NON-VANISHING MAGNETIC QUANTUM NUMBERS ARE
C  CALCULATED WITH THE HELP OF A 3-TERM RECURSION EQUATION IN L1. FOR
C  A DISCUSSION OF THIS RECURSION EQUATION AND THE STABILITY PROBLEMS
C  ASSOCIATED WITH IT SEE K.SCHULTEN AND R.G.GORDON, J.MATH.PHYS.16,
C  1961-1970 (1975) AND IBID. 16,01971-1988 (1975).
C
C  FOR THE SAKE OF THE NUMERICAL STABILITY THE RECURSION WILL PROCEED
C  SIMULTANEOUSLY FORWARDS AND BACKWARDS, STARTING FROM L1MIN =
C  MAX0(IABS(L2-L3),IABS(M1)) AND L1MAX = L2 + L3, RESPECTIVELY.
C  THE TWO RECURSIONS PROCEED IN EITHER DIRECTION AS LONG AS NUMERICAL
C  STABILITY IS GUARANTEED.
C
C  AT THE UPPER AND THE LOWER BOUNDARY OF THE DOMAIN OF ALLOWED L1-
C  VALUES THE 3-TERM RECURSION EQUATION REDUCES TO A 2-TERM RECURSION
C  EQUATION. THEREFORE, FOR THE TWO 3JM-SYMBOLS AT THE BOUNDARY OF
C  THE L1-DOMAIN WE CAN CHOSE  ARBITRARY STARTING VALUES. THE TWO
C  RECURSION SERIES ARE MATCHED AT THREE INTERMEDIATE POINTS BY LEAST
C  SQUARE FIT. THE REMAINING INDETERMINACY CAN THEN BE REMOVED WITH
C  THE HELP OF THE NORMALIZING RELATIONSHIP OF THE 3JM-SYMBOLS.
C
C  IN ORDER TO MINIMIZE THE NUMBER OF SQUARE ROOT EVALUATIONS WE DO NOT
C  COMPUTE THE PARITY COEFFICIENTS DIRECTLY BUT INSTEAD THE PARITY
C  COEFFICIENTS MULTIPLIED BY DSQRT(2*L1+1). THESE QUANTITIES ARE
C  CALCULATED USING A HOMOGENEOUS 2-TERM RECURSION EQUATION WHICH CAN
C  BE DERIVED FROM THE HOMOGENEOUS 3-TERM RECURSION EQUATION USED FOR
C  THE COMPUTATION OF THE GENERAL 3JM-SYMBOLS.
C  IT SHOULD BE NOTED THAT UNLIKE AS IN THE GENERAL CASE THE 2-TERM
C  RECURSION IS NUMMERICALLY STABLE IN EITHER DIRECTION.
C  THE RECURSION STARTS AT L1 = IABS(L2-L3) WITH AN ARBITRARY STARTING
C  VALUE. THE REMAINING INDETERMINACY IS THEN REMOVED WITH THE HELP OF
C  THE NORMALIZATION RELATIONSHIP OF THE PARITY COEFFICIENTS.
C
C  THE GAUNT COEFFICIENTS ARE THEN CALCULATED ACCORDING TO EQ.(1.19)
C  IN M.ROTENGERG ET AL. , THE 3J- AND 6J-SYMBOLS.
C
C  IF GAUNT-COEFFICIENTS OF THE TYPE ( L3 0 / L2 0 / L1 0 ) HAVE TO BE
C  CALCULATED WE USE THE SQUARED FORM OF THE 2-TERM RECURSION EQUATION
C  TO CALCULATE THE SQUARES OF THE 3JM-SYMBOLS WITH M1 = M2 = M3 = 0
C  DIRECTLY.
C
C
C  LIST OF PARAMETERS:
C  ------------------
C
C  INPUT PARAMETERS:
C
C  L2    : ANGULAR MOMENTUM QUANTUM NUMBER.
C  M2    : MAGNETIC QUANTUM NUMBER.
C  L3    : ANGULAR MOMENTUM QUANTUM NUMBER.
C  M3    : MAGNETIC QUANTUM NUMBER.
C  LARRAY: LENGTH OF THE ARRAY ARGNT.
C
C  OUTPUT PARAMETERS:
C
C  L1MIN : MINIMUM VALUE OF THE ANGULAR MOMENTUM QUANTUM NUMBER L1.
C  L1MAX : MAXIMUM VALUE OF THE ANGULAR MOMENTUM QUANTUM NUMBER L1.
C  M1    : MAGNETIC QUANTUM NUMBER.
C  NGAUNT: NUMBER OF GAUNT COEFFICIENTS CALCULATED AND STORED.
C  ARGNT : 1-DIMENSIONAL ARRAY CONTAINING THE GAUNT-COEFFICIENTS.
C
C  MACHINE-DEPENDENT QUANTITIES:
C
C  HUGE,TINY,SRHUGE,SRTINY,PI4.
C
C  TINY SHOULD BE SET CLOSE TO BUT NOT IDENTICAL WITH THE SMALLEST
C  FLOATING POINT NUMBER THAT IS REPRESENTABLE ON THE COMPUTER
C  SRTINY IS THE SQUARE ROOT OF TINY.
C
C  HUGE SHOULD BE SET CLOSE TO BUT NOT IDENTICAL WITH THE LARGEST
C  FLOATING POINT NUMBER THAT IS REPRESENTABLE ON THE COMPUTER
C  SRHUGE IS THE SQUARE ROOT OF HUGE.
C
C  IN ADDITION IT IS ASSUMED THAT HUGE = TINY**(-1) HOLDS.
C
C  PI4 ( CORRESPONDING TO 4.D0*PI ).
C
C**********************************************************************
      IMPLICIT REAL*8(A-H,O-Z)
C.... DIMENSION ARGNT(LARRAY)

      DIMENSION ARGNT(*)
C
      DATA TINY,SRTINY,HUGE,SRHUGE / 1.D-60,1.D-30,1.D+60,1.D+30 /
C
      DATA PI4 / .12566 37061 43591 72953 85057 D+02 /
C
C  CHECK RELATIVE MAGNITUDES OF L- AND M-VALUES.
C
      IF(L2.LT.IABS(M2))  GO TO 360
      IF(L3.LT.IABS(M3))  GO TO 360
C
C  CALCULATE M1 AND DETERMINE THE LIMITS FOR L1.
C
      M1 = M3 - M2
      L1MAX = L2 + L3
      L1MIN = 2*((L1MAX + MAX0(IABS(L2-L3),IABS(M1))+1)/2) - L1MAX
C
C  DETERMINE THE NUMBER OF NON-VANISHING GAUNT-COEFFICIENTS.
C
      NGAUNT = (L1MAX-L1MIN)/2 + 1
C
C  CHECK THE WHETHER DIMENSION OF ARGNT IS LARGE ENOUGH TO STORE ALL TH
C  GAUNT-COEFFICIENTS REQUIRED.
C
C.... IF(LARRAY.LT.NGAUNT)  GO TO 370
C
C  CHECK WHETHER ALL ANGULAR MOMENTUM QUANTUM NUMBERS VANISH,
C  I.E. L1 = L2 = L3 = 0.
C
      IF(L1MAX.EQ.0)  GO TO 340
C
C  CALCULATION OF SOME VARIABLES WHICH ASSUME CONSTANT VALUES DURING
C  THE RECURSIVE CALCULATION OF THE UNNORMALIZED 3JM-SYMBOLS.
C
      LA = (L2-L3)*(L2-L3)
      LB = (L2+L3+1)*(L2+L3+1)
C
C  CHECK WHETER M1 = M2 = M3 = 0 HOLDS.
C
      IF(M2.NE.0)  GO TO 30
      IF(M3.NE.0)  GO TO 30
C
C----------------------------------------------------------------------
C
C  THIS IS REACHED IF M1 = M2 = M3 = 0 HOLDS.
C
C  THE RECURSION EQUATION USED HERE IS A 2-TERM RECURSION EQUATION WHIC
C  CAN BE OBTAINED BY SQUARING THE 2-TERM RECURSION SATISFIED BY THE
C  PARITY COEFFICIENTS.
C
C  INITIALIZATION OF THE ANGULAR MOMENTUM QUANTUM NUMBER L1.
C
      L1 = L1MIN
      X1 = SRTINY
      DL = DFLOAT(L1+L1+1)
      SUM = X1*DL
C
C  FOR THE SAKE OF EFFICIENCY THE SQUARES OF THE UNNORMALIZED PARITY
C  COEFFICIENTS ARE NOW MULTIPLIED BY THE SQUARE ROOT OF 2*L1 + 1.
C
      ARGNT(1) = X1*DSQRT(DL)
C
C  RECURSIVE CALCULATION OF THE SQUARES OF THE OTHER
C  UNNORMALIZED PARITY COEFFICIENTS.
C
      DO 10 IGAUNT = 2,NGAUNT
C
C  CALCULATION OF SOME VARIABLES THAT ARE REQUIRED FOR THE RECURSIVE
C  CALCULATION OF THE NEXT SQUARED PARITY COEFFICIENT.
C
      L1 = L1 + 2
      L1P2 = L1*L1
      LP1 = (L1-1)*(L1-1)
C
C  RECURSIVE CALCULATION OF THE NEXT SQUARED PARITY COEFFICIENT.
C
	X1 = DFLOAT((LP1-LA)*(LB-LP1))/DFLOAT((L1P2-LA)*(LB-L1P2))*X1
	DL = DFLOAT(L1+L1+1)
	SUM = SUM + DL*X1
	ARGNT(IGAUNT) = X1*DSQRT(DL)
10    CONTINUE
C
C  CALCULATION OF THE NORMALIZATION CONSTANT.
C
      SUM = DSQRT(DFLOAT((L2+L2+1)*(L3+L3+1))/PI4)/SUM
C
C  NORMALIZE THE STRING OF GAUNT COEFFICIENTS ( L3 0 / L2 0 / L1 0 )
C
      DO 20 IGAUNT = 1,NGAUNT
	ARGNT(IGAUNT) = ARGNT(IGAUNT)*SUM
20    CONTINUE
      RETURN
C
C  END OF THE RECURSIVE CALCULATION OF THOSE GAUNT COEFFICIENTS WHERE
C  M1 = M2 = M3 = 0 HOLDS.
C
C----------------------------------------------------------------------
C
C  THIS IS REACHED IF NOT ALL OF THE MAGNETIC QUANTUM NUMBERS M1, M2 AN
C  M3 VANISH.
C
30    CONTINUE
C
C  DETERMINE THE MINIMAL VALUE WHICH L1 CAN ASSUME DURING THE RECURSIVE
C  CALCULATION OF THE UNNORMALIZED 3JM-SYMBOLS WITH NON-VANISHING
C  MAGNETIC QUANTUM NUMBERS.
C
      MIN3JM = MAX0(IABS(L2-L3),IABS(M1))
C
C  CHECK WHETHER MIN3JM = L1MAX HOLDS, I.E. ONLY ONE 3JM-SYMBOL WITH
C  NON-VANISHING MAGNETIC QUANTUM NUMBERS HAS TO BE CALCULATED.
C
      IF(MIN3JM.EQ.L1MAX)  GO TO 270
C
C  CALCULATION OF SOME VARIABLES WHICH ASSUME CONSTANT VALUES DURING TH
C  RECURSIVE CALCULATION OF THE UNNORMALIZED 3JM-SYMBBOLS WITH NON-
C  VANISHING MAGNETIC QUANTUM NUMBERS.
C
      LABM = M1*(L2*(L2+1)-L3*(L3+1))
      MM = M2 + M3
      M1P2 = M1*M1
C
C  INITIALIZATION OF THE DUMMY VARIABLE L1.
C
      L1 = MIN3JM
C
C  CHECK WHETHER MIN3JM = L1MIN HOLDS.
C
      IF(MIN3JM.EQ.L1MIN)  GO TO 50
C
C  THIS IS REACHED IF L1MIN = MIN3JM + 1 HOLDS. IN THAT CASE THE FIRST
C  UNNORMALIZED 3JM-SYMBOL CALCULATED WILL NOT BE STORED IN ARGNT.
C
C  CALCULATION OF THE FIRST UNNORMALIZED 3JM-SYMBOL
C
      X3 = SRTINY
      L12 = L1 + L1 + 1
      SUMFOR = DFLOAT(L12)*TINY
C
C  CALCULATION OF SOME VARIABLES REQUIRED FOR THE RECURSIVE
C  CALCULATION OF THE SECOND UNNORMALIZED 3JM-SYMBOL.
C
      L1 = L1 + 1
      L1P2 = L1*L1
      FACOLD = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
      C2 = 1.D0/(DFLOAT(L1-1)*FACOLD)
      C1 = C2*DFLOAT(L12*(LABM+MM*L1*(L1-1)))
C
C  RECURSIVE CALCULATION OF THE SECOND UNNORMALIZED 3JM-SYMBOL.
C
      X2 = C1*X3
      C1OLD = DABS(C1)
      ARGNT(1) = X2
      L12 = L12 + 2
      SUMFOR = SUMFOR + DFLOAT(L12)*X2*X2
C
C  CHECK WHETHER L1 = L1MAX HOLDS.
C
      IF (L1.EQ.L1MAX)  GO TO 260
C
C  RECURSIVE CALCULATION OF THE NEXT 3JM-SYMBOL.
C
C
C  CALCULATION OF SOME COEFFICIENTS THAT ARE REQUIRED FOR THE RECURSIVE
C  CALCULATION OF THE NEXT 3JM-SYMBOL.
C
      L1 = L1 + 1
      L1P2 = L1*L1
      FACNEW = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
      C2 = 1.D0/(DFLOAT(L1-1)*FACNEW)
      C1 = C2*DFLOAT(L12*(LABM+MM*L1*(L1-1)))
C
C  RECURSIVE CALCULATION OF THE THIRD UNNORMALIZED 3JM-SYMBOL.
C
      X1 = C1*X2 - DFLOAT(L1)*FACOLD*C2*X3
C
C  CHECK WHETHER THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C
      IF (DABS(X1).LT.SRHUGE)  GO TO 40
C
C  THIS IS REACHED IF THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C  IN THAT CASE THE 3JM-SYMBOLS ALREADY CALCULATED AND SUMFOR HAVE TO B
C  RESCALED IN ORDER TO PREVENT OVERFLOW.
C
	X1 = X1*SRTINY
	X2 = X2*SRTINY
	ARGNT(1) = ARGNT(1)*SRTINY
	SUMFOR = SUMFOR*TINY
40    CONTINUE
C
C  CHECK WHETHER DABS(C1) IS DECREASING.
C  AS LONG AS DABS(C1) IS DECREASING THE RECURSION PROCEEDS TOWARDS
C  INCREASING 3JM-VALUES AND IS NUMERICALLY STABLE. ONCE AN INCREASE
C  OF DABS(C1) IS DETECTED THE RECURSION DIRECTION IS REVERSED.
C
      C1NEW = DABS(C1)
      IF (C1NEW.GE.C1OLD)  GO TO 130
C
C  CALCULATION OF SUMFOR.
C
	L12    = L12 + 2
	SUMFOR = SUMFOR + DFLOAT(L12)*X1*X1
      GO TO 70
C
C  THIS REACHED IF L1MIN = MIN3JM HOLDS.
C  IN THAT CASE THE FIRST 3JM-SYMBOL CALCULATED IS ALSO NEEDED FOR THE
C  COMPUTATION OF THE FIRST GAUNT COEFFICIENT AND HAS TO BE STORED IN
C  THE ARRAY ARGNT.
C
50    CONTINUE
C
C  CALCULATION OF THE FIRST UNNORMALIZED 3JM-SYMBOL.
C
      X2 = SRTINY
      L12 = L1 + L1 + 1
      SUMFOR = DFLOAT(L12)*TINY
      ARGNT(1) = X2
C
C  CHECK WHETHER THE RECURSION STARTS AT L1 = 0.
C
      IF (L1MIN.EQ.0)  GO TO 60
C
C  RECURSIVE CALCULATION OF THE SECOND UNNORMALIZED 3JM-SYMBOL.
C
      L1 = L1 + 1
      L1P2 = L1*L1
      FACNEW = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
      C1 = DFLOAT(L12*(LABM+MM*L1*(L1-1)))/(DFLOAT(L1-1)*FACNEW)
      X1 = C1*X2
      L12 = L12 + 2
      SUMFOR = SUMFOR + DFLOAT(L12)*X1*X1
      C1NEW = DABS(C1)
      GO TO 70
C
C  THIS IS REACHED IF L1MIN = 0 HOLDS. IN THAT CASE THE 2-TERM-RECURSIO
C  EQUATION WHICH HOLDS AT THE LOWER BOUNDARY OF THE ALLOWED L1-DOMAIN
C  HAS TO BE MODIFIED.
C
60    CONTINUE
C
C  CALCULATION OF THE SECOND 3JM-SYMBOL.
C
      L1 = 1
      L12 = 3
      C1 = DFLOAT(M2)/DSQRT(DFLOAT(L2*(L2+1)))
      X1 = C1*X2
      SUMFOR = SUMFOR + 3.D0*X1*X1
      FACNEW = DSQRT(DFLOAT(LB-1))
      C1NEW = DABS(C1)
70    CONTINUE
C
C  INITIALIZATION OF THE DUMMY VARIABLE IGAUNT.
C
      IGAUNT = 1
C
C  RECURSIVE CALCULATION OF THE REMAINING UNNORMALIZED 3JM-SYMBOLS.
C
80    CONTINUE
C
C  CALCULATION OF SOME VARIABLES AND COEFFICIENTS THAT ARE REQUIRED FOR
C  THE RECURSIVE CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	FACOLD = FACNEW
	C1OLD  = C1NEW
	X3     = X2
	X2     = X1
	L1     = L1 + 1
	L1P2    = L1*L1
	FACNEW = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
	C2     = 1.D0/(DFLOAT(L1-1)*FACNEW)
	C1     = C2*DFLOAT(L12*(LABM+MM*L1*(L1-1)))
	L12    = L12 + 2
C
C  RECURSIVE CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	X1     = C1*X2 - DFLOAT(L1)*FACOLD*C2*X3
C
C  CHECK WHETHER THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C
	IF (DABS(X1).LT.SRHUGE)  GO TO 100
C
C  THIS IS REACHED IF THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C  IN THAT CASE THE 3JM-SYMBOLS ALREADY CALCULATED AND SUMFOR HAVE TO B
C  RESCALED IN ORDER TO PREVENT OVERFLOW.
C
	  X1 = X1*SRTINY
	  X2 = X2*SRTINY
	  X3 = X3*SRTINY
	  DO 90 I = 1,IGAUNT
	    ARGNT(I) = ARGNT(I)*SRTINY
90        CONTINUE
	  SUMFOR = SUMFOR*TINY
100     CONTINUE
C
C  CHECK WHETHER L1 = L1MAX HOLDS.
C
	IF (L1.EQ.L1MAX)  GO TO 250
C
C  CHECK WHETHER DABS(C1) IS DECREASING.
C  AS LONG AS DABS(C1) IS DECREASING THE RECURSION PROCEEDS TOWARDS
C  INCREASING 3JM-VALUES AND IS NUMERICALLY STABLE. ONCE AN INCREASE
C  OF DABS(C1) IS DETECTED THE RECURSION DIRECTION IS REVERSED.
C
	C1NEW = DABS(C1)
	IF (C1NEW.GE.C1OLD)  GO TO 130
	IGAUNT = IGAUNT + 1
	SUMFOR = SUMFOR + DFLOAT(L12)*X1*X1
C
C  STORAGE OF THOSE UNNORMALIZED 3JM-SYMBOL WITH NON-VANISHING MAGNETIC
C  QUANTUM NUMBERS M1, M2 AND M3 THAT ARE REQUIRED FOR THE CALCULATION
C  OF THE GAUNT COEFFICIENTS IN THE ARRAY ARGNT.
C
	ARGNT(IGAUNT) = X1
C
C  CALCULATION OF SOME VARIABLES AND COEFFICIENTS THAT ARE REQUIRED FOR
C  THE RECURSIVE CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	FACOLD = FACNEW
	C1OLD  = C1NEW
	X3     = X2
	X2     = X1
	L1     = L1 + 1
	L1P2    = L1*L1
	FACNEW = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
	C2     = 1.D0/(DFLOAT(L1-1)*FACNEW)
	C1     = C2*DFLOAT(L12*(LABM+MM*L1*(L1-1)))
C
C  RECURSIVE CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	X1     = C1*X2 - DFLOAT(L1)*FACOLD*C2*X3
C
C  CHECK WHETHER THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C
	IF (DABS(X1).LT.SRHUGE)  GO TO 120
C
C  THIS IS REACHED IF THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C  IN THAT CASE THE 3JM-SYMBOLS ALREADY CALCULATED AND SUMFOR HAVE TO B
C  RESCALED IN ORDER TO PREVENT OVERFLOW.
C
	  X1 = X1*SRTINY
	  X2 = X2*SRTINY
	  X3 = X3*SRTINY
	  DO 110 I = 1,IGAUNT
	    ARGNT(I) = ARGNT(I)*SRTINY
110       CONTINUE
	  SUMFOR = SUMFOR*TINY
120     CONTINUE
C
C  CHECK WHETHER DABS(C1) IS DECREASING.
C  AS LONG AS DABS(C1) IS DECREASING THE RECURSION PROCEEDS TOWARDS
C  INCREASING 3JM-VALUES AND IS NUMERICALLY STABLE. ONCE AN INCREASE
C  OF DABS(C1) IS DETECTED THE RECURSION DIRECTION IS REVERSED.
C
	C1NEW = DABS(C1)
	IF (C1NEW.GE.C1OLD)  GO TO 130
	L12 = L12 + 2
	SUMFOR = SUMFOR + DFLOAT(L12)*X1*X1

      GO TO 80
C
C  THIS IS REACHED IF THE STABILITY OF THE RECURSION IS REVERSED.
C
130   CONTINUE
      LMATCH = L1 - 1
C
C  END OF FORWARD RECURSION.
C
C---------------------------------------------------------------------
C
C  START OF BACKWARD RECURSION.
C
C  RECURSIVE CALCULATION OF THE UNNORMALIZED 3JM-SYMBOLS STARTING AT
C  L1 = L1MAX IN DIRECTION OF DECREASING L1-VALUES UNTIL FORWARD AND
C  BACKWARD RECURSION OVERLAP AT THREE SUCCESSIVE POINTS:
C  L1 = LMATCH + 1, L1 = LMATCH AND L1 = LMATCH - 1.
C
C  INITIALIZATION OF THE DUMMY VARIABLE L1.
C
      L1 = L1MAX
C
C  CALCULATION OF THE FIRST 3JM-SYMBOL.
C
      Y2     = SRTINY
      L12    = L1 + L1 + 1
      SUMBAC = DFLOAT(L12)*TINY
C
C  STORAGE OF THE FIRST UNNORMALIZED 3JM-SYMBOL IN THE ARRAY ARGNT.
C
      ARGNT(NGAUNT) = Y2
C
C  CALCULATION OF THE SECOND UNNORMALIZED 3JM-SYMBOL WITH THE HELP OF
C  THE SIMPLIFIED 2-TERM RECURSION EQUATION WHICH HOLDS AT THE UPPER
C  BOUNDARY OF THE DOMAIN OF ALLOWED L1-VALUES.
C
      L1P2 = L1*L1
      FACNEW = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
      Y3 = DFLOAT(L12*(LABM+MM*L1*(L1+1)))/(DFLOAT(L1+1)*FACNEW)*Y2
      L1 = L1 - 1
C
C  INITIALIZATION OF THE DUMMY VARIABLE KGAUNT.
C
      KGAUNT = NGAUNT
C
C  CALCULATION OF THE OTHER 3JM-SYMBOLS REQUIRED USING BACKWARD
C  RECURSION.
C
140   CONTINUE
	L12 = L12 - 2
	SUMBAC = SUMBAC + DFLOAT(L12)*Y3*Y3
C
C  CALCULATION OF SOME VARIABLES AND COEFFICIENTS THAT ARE REQUIRED FOR
C  THE RECURSIVE CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	L1P2   = L1*L1
	Y1     = Y2
	Y2     = Y3
	FACOLD = FACNEW
	FACNEW = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
C
C  CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	Y3 = (DFLOAT(L12*(LABM+MM*L1*(L1+1)))*Y2 -
     *       FACOLD*DFLOAT(L1)*Y1)/(DFLOAT(L1+1)*FACNEW)
	L1 = L1 - 1
C
C  CHECK WHETHER THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C
	IF (DABS(Y3).LT.SRHUGE)  GO TO 160
C
C  THIS IS REACHED IF THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C  IN THAT CASE THE 3JM-SYMBOLS ALREADY CALCULATED AND SUMBAC HAVE TO B
C  RESCALED IN ORDER TO PREVENT OVERFLOW.
C
	  Y3 = Y3*SRTINY
	  Y2 = Y2*SRTINY
	  DO 150 I = KGAUNT,NGAUNT
	    ARGNT(I) = ARGNT(I)*SRTINY
150       CONTINUE
	  SUMBAC = SUMBAC*TINY
160     CONTINUE
C
C  CHECK WHETHER L1 = LMATCH HOLDS.
C
      IF (L1.EQ.LMATCH)  GO TO 190
C
C  STORAGE OF THE UNNORMALIZED 3JM-SYMBOL WITH NON-VANISHING MAGNETIC
C  QUANTUM NUMBERS M1, M2 AND M3 REQUIRED FOR THE CALCULATION OF THE
C  GAUNT COEFFICIENT IN THE ARRAY ARGNT.
C
	KGAUNT = KGAUNT - 1
	ARGNT(KGAUNT) = Y3
C
C  CALCULATION OF SUMBAC.
C
	L12 = L12 - 2
	SUMBAC = SUMBAC + DFLOAT(L12)*Y3*Y3
C
C  CALCULATION OF SOME VARIABLES AND COEFFICIENTS THAT ARE REQUIRED FOR
C  THE RECURSIVE CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	L1P2 = L1*L1
	Y1     = Y2
	Y2     = Y3
	FACOLD = FACNEW
	FACNEW = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
C
C  CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	Y3 = (DFLOAT(L12*(LABM+MM*L1*(L1+1)))*Y2 -
     *        FACOLD*DFLOAT(L1)*Y1)/(DFLOAT(L1+1)*FACNEW)
	L1 = L1 - 1
C
C  CHECK WHETHER THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C
	IF (DABS(Y3).LT.SRHUGE)  GO TO 180
C
C  THIS IS REACHED IF THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C  IN THAT CASE THE 3JM-SYMBOLS ALREADY CALCULATED AND SUMBAC HAVE TO B
C  RESCALED IN ORDER TO PREVENT OVERFLOW.
C
	  Y3 = Y3*SRTINY
	  Y2 = Y2*SRTINY
	  DO 170 I = KGAUNT,NGAUNT
	    ARGNT(I) = ARGNT(I)*SRTINY
170       CONTINUE
	  SUMBAC = SUMBAC*TINY
180     CONTINUE
C
C  CHECK WHETHER L1 = LMATCH HOLDS.
C
      IF (L1.EQ.LMATCH)  GO TO 190
C
C  THIS IS REACHED IF NOT ALL 3JM-SYMBOLS REQUIRED HAVE BEEN CALCULATED
C  USING BACKWARD RECURSION.
C
      GO TO 140
C
C  THIS IS REACHED IF L1 = LMATCH HOLDS AND IF ALL BUT ONE OF THE
C  UNNORMALIZED 3JM-SYMBOLS REQUIRED ARE CALCULATED WITH THE
C  HELP OF BACKWARD RECURSION.
C
190   CONTINUE
C
C  CALCULATION OF SOME VARIABLES AND COEFFICIENTS THAT ARE REQUIRED FOR
C  THE RECURSIVE CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	L12    = L12 - 2
	L1P2   = L1*L1
	Y1     = Y2
	Y2     = Y3
	FACOLD = FACNEW
	FACNEW = DSQRT(DFLOAT((L1P2-LA)*(LB-L1P2)*(L1P2-M1P2)))
C
C  CALCULATION OF THE NEXT UNNORMALIZED 3JM-SYMBOL.
C
	Y3 = (DFLOAT(L12*(LABM+MM*L1*(L1+1)))*Y2 -
     *        FACOLD*DFLOAT(L1)*Y1)/(DFLOAT(L1+1)*FACNEW)
C
C  CHECK WHETHER THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C
	IF (DABS(Y3).LT.SRHUGE)  GO TO 210
C
C  THIS IS REACHED IF THE LAST UNNORMALIZED 3JM-SYMBOL EXCEEDS SRHUGE.
C  IN THAT CASE THE 3JM-SYMBOLS ALREADY CALCULATED AND SUMBAC HAVE TO B
C  RESCALED IN ORDER TO PREVENT OVERFLOW.
C
	  Y3 = Y3*SRTINY
	  Y2 = Y2*SRTINY
	  DO 200 I = KGAUNT,NGAUNT
	    ARGNT(I) = ARGNT(I)*SRTINY
200       CONTINUE
	  SUMBAC = SUMBAC*TINY
210     CONTINUE
C
C  END OF BACKWARD RECURSION.
C
C----------------------------------------------------------------------
C
C  DETRMINATION OF THE NORMALIZATION CONSTANT.
C
C  DETERMINE NOW RATIO SUCH THAT YI = XI*RATIO (I = 1,2,3) HOLDS WITH
C  MINIMAL ERROR.
C
      RATIO = (X1*Y1 + X2*Y2 + X3*Y3)/(X1*X1 + X2*X2 + X3*X3)
C
C  CHECK WHETHER DABS(RATIO) IS LESS THAN ONE.
C
      IF (DABS(RATIO).LT.1.D0)  GO TO 230
C
C  THIS IS REACHED IF DABS(RATIO) IS GREATER THAN ONE.
C
      IGAUNT = KGAUNT - 1
      DO 220 I = 1,IGAUNT
	ARGNT(I) = ARGNT(I)*RATIO
220   CONTINUE
      SUM = RATIO*RATIO*SUMFOR + SUMBAC
      GO TO 280
C
C  THIS IS REACHED IF DABS(RATIO) IS LESS THAN ONE.
C
230   CONTINUE
      RATIO = 1.D0/RATIO
      DO 240 I = KGAUNT,NGAUNT
	ARGNT(I) = ARGNT(I)*RATIO
240   CONTINUE
      SUM = SUMFOR + RATIO*RATIO*SUMBAC
      GO TO 280
C
C  THIS IS REACHED IF ALL UNNORMALIZED 3JM-SYMBOLS WITH NON-VANISHING
C  MAGNETIC QUANTUM NUMBERS WERE CALCULATED WITH UPWARD REDURSION AND I
C  THE LAST 3JM-SYMBOL HAS TO BE STORED IN THE ARRAY ARGNT.
C
250   CONTINUE
	SUM = SUMFOR + DFLOAT(L12)*X1*X1
	ARGNT(NGAUNT) = X1
      GO TO 280
C
C  THIS IS REACHED IF ALL UNNORMALIZED 3JM-SYMBOLS WITH NON-VANISHING
C  MAGNETIC QUANTUM NUMBERS M1, M2 AND M3 WERE CALCULATED WITH UPWARD
C  RECURSION.
260   CONTINUE
      SUM = SUMFOR
      GO TO 280
C
C  THIS IS REACHED IF ONLY ONE 3JM-SYMBOL WITH NON-VANISHING MAGNETIC
C  QUANTUM NUMBERS M1, M2 AND M3 HAS TO BE CALCULATED, BUT MORE THAN
C  ONE PARITY COEFFICIENT HAS TO BE CALCULATED.
C
270   CONTINUE
C
C  CHECK WHETHER L2 = 0 HOLDS.
C
      IF(L2.EQ.0)  GO TO 340
C
C  CHECK WHETHER L3 = 0 HOLDS.
C
      IF(L3.EQ.0)  GO TO 350
      ARGNT(1) = 1.D0/DSQRT(DFLOAT(L1MAX + L1MAX + 1))
      SUM = 1.D0
C----------------------------------------------------------------------
C
C  START OF THE RECURSIVE CALCULATION OF THE SO-CALLED PARITY
C  COEFFICIENTS, I.E. 3JM-SYMBOLS WITH M1 = M2 = M3 = 0.
C
280   CONTINUE
C
C  DETERMINE LOWER LIMIT OF THE L1-DOMAIN OF THE PARITY COEFFICIENTS.
C
      MINPAR = IABS(L2-L3)
C
C  INITIALIZATION OF THE DUMMY VARIABLES L1 AND KGAUNT.
C
      L1 = MINPAR
      KGAUNT = 1
C
C  CALCULATION OF THE FIRST UNNORMALIZED PARITY COEFFICIENT.
C
      X1 = SRTINY
      SUMP = TINY
      LNEW = L1 + L1 + 1
C
C  CHECK WHETHER MINPAR = L1MIN HOLDS.
C
      IF (MINPAR.LT.L1MIN)  GO TO 290
C
C  THIS IS REACHED IF MINPAR = L1MIN HOLDS. IN THAT CASE THE FIRST
C  UNNORMALIZED PARITY COEFFICIENT IS REQUIRED FOR THE CALCULATION OF
C  THE FIRST GAUNT COEFFICIENT AND HAS TO BE STORED IN THE ARRAY ARGNT.
C
	ARGNT(1) = ARGNT(1)*X1
	KGAUNT = KGAUNT + 1
290   CONTINUE
C
C  CHECK WHETHER THERE ARE SOME MORE UNNORMALIZED PARITY COEFFICIENTS T
C  BE CALCULATED THAT ARE ONLY REQUIRED FOR THE CALCULATION OF THE
C  NORMALIZATION CONSTANT OR WHETHER THE NEXT UNNORMALIZED PARITY
C  COEFFICIENT WILL ALSO BE REQUIRED FOR THE CALCULATION OF THE FIRST
C  GAUNT COEFFICIENT OR WHETHER ALL UNNORMALIZED PARITY COEFFICIENT WIL
C  ALSO BE REQUIRED FOR THE CALCULATION OF THE GAUNT COEFFICIENTS.
C
      IF ((MINPAR+2).GE.L1MIN)  GO TO 310
C
C  THIS IS REACHED IF THERE ARE STILL SOME MORE UNNORMALIZED PARITY
C  COEFFICIENTS TO BE CALCULATED THAT WILL ONLY BE REQUIRED FOR THE
C  CALCULATION OF THE NORMALIZATION CONSTANT.
C
C  DETERMINE THE NUMBER OF UNNORMALIZED PARITY COEFFICIENTS THAT ARE TO
C  BE CALCULATED AND THAT ARE ONLY REQUIRED FOR THE CALCULATION OF THE
C  NORMALIZATION CONSTANT OF THE RECURSION STRING.
C
	NPAR = (L1MIN-MINPAR)/2
C
C  CALCULATION OF THOSE UNNORMALIZED PARITY COEFFICIENTS THAT ARE ONLY
C  REQUIRED FOR THE CALCULATION OF THE NORMALIZATION CONSTANT.
C
	DO 300 I = 2,NPAR
	  L1 = L1 + 2
	  LP1 = (L1-1)*(L1-1)
	  L1P2 = L1*L1
	  LOLD = LNEW
	  LNEW = LNEW + 4
	  X1 = - DSQRT(DFLOAT(LNEW*(LP1-LA)*(LB-LP1))/
     *    DFLOAT(LOLD*(L1P2-LA)*(LB-L1P2)))*X1
	  SUMP = SUMP + X1*X1
300     CONTINUE
C
C  THIS IS REACHED IF THE NEXT UNNORMALIZED PARITY COEFFICIENT WILL
C  BE REQUIRED FOR THE CALCULATION OF THE GAUNT COEFFICIENTS.
C
310   CONTINUE
      DO 320 IGAUNT = KGAUNT,NGAUNT
	L1 = L1 + 2
	LP1 = (L1-1)*(L1-1)
	L1P2 = L1*L1
	LOLD = LNEW
	LNEW = LNEW + 4
	X1 = - DSQRT(DFLOAT(LNEW*(LP1-LA)*(LB-LP1))/
     *  DFLOAT(LOLD*(L1P2-LA)*(LB-L1P2)))*X1
	SUMP = SUMP + X1*X1
	ARGNT(IGAUNT) = ARGNT(IGAUNT)*X1
320   CONTINUE
C
C  END OF THE RECURSIVE CALCULATION OF THE SO-CALLED PARITY COEFFICIENT
C
C----------------------------------------------------------------------
C
C  CALCULATION OF THE NORMALIZATION CONSTANT OF THE STRING OF GAUNT
C  COEFFICIENTS.
C
      CNORM = DSQRT(DFLOAT((L2+L2+1)*(L3+L3+1))/PI4)/
     * (DSQRT(SUM)*DSQRT(SUMP))
C
C  CHECK WHETHER THE SIGN OF THE HIGHEST UNNORMALIZED GAUNT COEFFICIENT
C  IS CORRECT.
C
      IF (ARGNT(NGAUNT).NE.(-1)**M2*DABS(ARGNT(NGAUNT)))
     *  CNORM = - CNORM
C
C  NORMALIZE THE STRING OF GAUNT COEFFICIENTS ( L3 M3 / L2 M2 / L1 M1 )
C
      DO 330 IGAUNT = 1,NGAUNT
	ARGNT(IGAUNT) = ARGNT(IGAUNT)*CNORM
330   CONTINUE
      RETURN
C
C  THIS IS REACHED IF EITHER L1 = L2 = L3 = 0 OR L2 = 0 BUT NOT L3 = 0
C  HOLDS. IN THE FIRST CASE WE HAVE
C  ( 0 0 / 0 0 / 0 0 ) = 1./DSQRT(4*PI)
C  AND IN THE SECOND CASE WE HAVE
C  ( L3 M3 / 0 0 / L3 M3 ) = 1./DSQRT(4*PI).
C
340   CONTINUE
	ARGNT(1) = 1.D0/DSQRT(PI4)
      RETURN
C
C  THIS IS REACHED IF L3 = 0 HOLDS.
C  IN THAT CASE WE HAVE
C  ( L2 -M2 / 0 0 / L2 -M2 ) = (-1)**M2/DSQRT(PI4)
C
350   CONTINUE
	ARGNT(1) = (-1)**M2/DSQRT(PI4)
      RETURN
C
C----------------------------------------------------------------------
C
C  ERROR SECTION
C  -------------
C
C  THIS REACHED IF ILLEGAL ANGULAR MOMENTUM QUANTUM NUMBERS L2, M2, L3
C  AND M3 WERE USED AS INPUT PARAMETERS.
C
360   CONTINUE

      WRITE(4,1000)
1000  FORMAT(1H0,34H *** ERROR IN SUBROUTINE GAUNT ***/2H  ,33(1H-))
      WRITE(4,1010) L2,M2,L3,M3
1010  FORMAT(1H0,70HILLEGAL ANGULAR MOMENTUM QUANTUM NUMBERS WERE USED
     1S INPUT PARAMETERS /
     2 1H0,5HL2 = ,I4,7H  M2 = ,I5,7H  L3 = ,I4,7H  M3 = ,I5)
      STOP
C
C  THIS IS REACHED IF DIMENSION OF ARGNT IS NOT LARGE ENOUGH TO STORE
C  ALL THE GAUNT COEFFICIENTS REQUIRED, I.E. NGAUNT IS GREATER
C  THAN LARRAY.
C
370   CONTINUE
      WRITE(4,1000)
      WRITE(4,1020) LARRAY,NGAUNT
1020  FORMAT(1H0,82H  DIMENSION OF ARGNT NOT LARGE ENOUGH TO STORE ALL
     1HE GAUNT COEFFICIENTS REQUIRED/
     2 1H0,9HLARRAY = ,I4,12H   NGAUNT = ,I4)
      STOP
      END
